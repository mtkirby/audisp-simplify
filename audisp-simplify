#!/bin/env perl
################################################################################
# 20130928 Kirby  v1.0
# 20131001 Kirby  v1.0.1
# 20131004 Kirby  v1.0.2
# 20131006 Kirby  v1.0.3
# 20140314 Kirby  v1.0.4
# 20150628 Kirby  v1.0.5
# 20150724 Kirby  v1.0.6
# 20150728 Kirby  v1.0.7
# 20161007 Kirby  v1.0.8
# 20170129 Kirby  v1.0.9
# 20170129 Kirby  v1.0.10
# 20170130 Kirby  v1.0.11
# 20170130 Kirby  v1.0.12
# 20170131 Kirby  v2.0
# 20170201 Kirby  v2.0.1
# 20170202 Kirby  v2.0.2
# 20170204 Kirby  v2.0.3
# 20170206 Kirby  v2.0.4
# 20170216 Kirby  v2.0.5
# 20170515 Kirby  v2.0.6
# 20170522 Kirby  v2.0.7
# 20201004 Kirby  v2.0.8
################################################################################

# LICENSE
#
#    audisp-simplify is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    audisp-simplify is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with audisp-simplify; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use POSIX qw(strftime tzset);
use Errno;
use strict;
umask 0077;

# enable if timezone has problems
#$ENV{TZ} = strftime( "%Z", localtime() );
#tzset;

# https://raw.githubusercontent.com/linux-audit/audit-documentation/master/specs/fields/field-dictionary.csv
# https://access.redhat.com/documentation/en-US/Red_Hat_Enterprise_Linux/6/html/Security_Guide/app-Audit_Reference.html#sec-Audit_Events_Fields
# https://fedorahosted.org/audit/browser/trunk/rules/30-pci-dss-v31.rules

&Main;

#################################################################################
sub Main {
    my @alarms;
    my $argc;
    my $args;
    my $auditid;
    my $cmd;
    my @command;
    my @cleancmd;
    my $err;
    my %errcode;
    my %errdesc;
    my $errno;
    my $event;
    my $ignorefile = '/etc/audisp/simplify.ignores';
    my $ignorekey;
    my %ignores;
    my $ignorestring;
    my $item;
    my $log;
    my $logfile = '/var/log/audisp-simplify';
    my $name;
    my $ouid_user;
    my %sh;
    my $subkey;
    my $time;

    #
    # Cache all the error codes rather than calling Errno on each event.
    #
    foreach $err ( keys(%!) ) {
        $!               = eval "Errno::$err";
        $errno           = $! + 0;
        $errcode{$errno} = $err;
        $errdesc{$err}   = "$!";
    }

    if ( -f "$ignorefile" ) {
        open( IG, '<', "$ignorefile" );
        while (<IG>) {
            chomp;
            next if ( $_ =~ /^#/ );
            ( $ignorekey, $ignorestring ) = split( /=/, $_ );
            $ignorekey =~ s/(^\s+|\s+$)//g;
            $ignorestring =~ s/(^\s+|\s+$)//g;
            push( @{ $ignores{$ignorekey} }, $ignorestring );
        }
        close(IG);
    }

    # type=PATH msg=audit(1542691793.976:3624): item=1 name="/lib64/ld-linux-x86-64.so.2" inode=67110215 dev=fd:00 mode=0100755 ouid=0 ogid=0 rdev=00:00 obj=system_u:object_r:ld_so_t:s0 nametype=NORMAL cap_fp=0000000000000000 cap_fi=0000000000000000 cap_fe=0 cap_fver=0
    #
    # type=EXECVE msg=audit(1542691923.009:3917): argc=4 a0="grep" a1="--quiet" a2="-e" a3="/dev/ttyS"



  INPUT:
    while (<STDIN>) {
        chomp;
        $event = $_;
        if ( $event =~ m| msg=audit\((\d+)\.(\d+):(\d+)\): | ) {
            $auditid                  = $3;
            $sh{$auditid}{auditid}    = $auditid;
            $sh{$auditid}{date}       = strftime "%Y-%m-%d", localtime( $1 );
            $sh{$auditid}{time}       = strftime "%H:%M:%S", localtime( $1 );
            $sh{$auditid}{time}       .= '.' . $2;
            $sh{$auditid}{timezone}   = strftime "%z", localtime( $1 );
            $sh{$auditid}{autime}     = time;
            $sh{$auditid}{addr}       = $1 if ( $event =~ m| addr=(\S+)| );
            $sh{$auditid}{exit}       = $1 if ( $event =~ m| exit=(-?\d+)| );
            $sh{$auditid}{hostname}   = $1 if ( $event =~ m| hostname=(\S+)| );
            $sh{$auditid}{key}        = $1 if ( $event =~ m| key="([^"]+)"| );
            $sh{$auditid}{newcontext} = $1 if ( $event =~ m| newcontext=(\S+)| );
            $sh{$auditid}{oldcontext} = $1 if ( $event =~ m| oldcontext=(\S+)| );
            $sh{$auditid}{pid}        = $1 if ( $event =~ m| pid=(\d+)| );
            $sh{$auditid}{ppid}       = $1 if ( $event =~ m| ppid=(\d+)| );
            $sh{$auditid}{res}        = $1 if ( $event =~ m| res=(\w+)| );
            $sh{$auditid}{seresult}   = $1 if ( $event =~ m| seresult=(\w+)| );
            $sh{$auditid}{ses}        = $1 if ( $event =~ m| ses=(\d+)| );
            $sh{$auditid}{ses}        = 'unset' if ( $event =~ m| ses=4294967295| );
            $sh{$auditid}{subj}       = $1 if ( $event =~ m| subj=(\S+)| );
            $sh{$auditid}{terminal}   = $1 if ( $event =~ m| terminal=(\S+)| );
            $sh{$auditid}{tty}        = $1 if ( $event =~ m| tty=(\S+)| );
            $sh{$auditid}{direction}  = $1 if ( $event =~ m| direction=(\S+)| );
            $sh{$auditid}{cipher}     = $1 if ( $event =~ m| cipher=(\S+)| );
            $sh{$auditid}{ksize}      = $1 if ( $event =~ m| ksize=(\d+)| );
            $sh{$auditid}{mac}        = $1 if ( $event =~ m| mac=(\S+)| );
            $sh{$auditid}{pfs}        = $1 if ( $event =~ m| pfs=(\S+)| );
            $sh{$auditid}{spid}       = $1 if ( $event =~ m| spid=(\d+)| );
            $sh{$auditid}{suid}       = $1 if ( $event =~ m| suid=(\d+)| );
            $sh{$auditid}{laddr}      = $1 if ( $event =~ m| laddr=(\S+)| );
            $sh{$auditid}{lport}      = $1 if ( $event =~ m| lport=(\d+)| );


            # avc:  denied  { execmem } for
            if ( $event =~ m| avc:\s+(\w+\s+\{.+\}) for | ) {
                $sh{$auditid}{avc} = $1;
            }

            if ( $event =~ m| uid=(\d+)| ) {
                $sh{$auditid}{uid} = $1;
                $sh{$auditid}{uid_user} = getpwuid($1) || 'unset';
            }
            if ( $event =~ m| auid=(\d+)| ) {
                $sh{$auditid}{auid} = $1;
                $sh{$auditid}{auid_user} = getpwuid($1) || 'unset';
            }
            if ( $event =~ m| euid=(\d+)| ) {
                $sh{$auditid}{euid} = $1;
                $sh{$auditid}{euid_user} = getpwuid($1) || 'unset';
            }

            if ( ( $event =~ m|^type=(\S+)| ) 
            or ( $event =~ m| type=(\S+)| ) ) {
                unless ( $sh{$auditid}{types} =~ m|$1| ) {
                    $sh{$auditid}{types} = defined $sh{$auditid}{types} ? "$sh{$auditid}{types},$1" : $1;
                }
            }


            #
            # Some keys are unpredictable.
            # Some have values wrapped in quotes, some don't, and some are encoded
            #
            if ( ( $event =~ m| name=\S+ | )
            and ( $event !~ m| obj=system_u:object_r:ld_so_t:s0| ) ) {
                if ( $event =~ m| name="([^"]+)" | ) {
                    $name = $1;
                } elsif ( $event =~ m| name=([A-F0-9]+) | ) {
                    $name = pack( "H*", "$1" );
                } elsif ( $event =~ m| name=(\S+) | ) {
                    $name = $1;
                }

                $sh{$auditid}{names} = defined $sh{$auditid}{names} ? "$sh{$auditid}{names},$name" : $name;
                if ( $event =~ m|mode=(\d+)| ) {
                    $sh{$auditid}{mode}{$name} = $1;
                }
                if ( $event =~ m|ouid=(\d+)| ) {
                    $sh{$auditid}{ouid}{$name} = $1;
                }
            }

            if ( ( $event =~ m| nametype=(\S+)| ) 
            and ( $event !~ m| obj=system_u:object_r:ld_so_t:s0| ) ) {
                $sh{$auditid}{nametypes} = defined $sh{$auditid}{nametypes} ? "$sh{$auditid}{nametypes},$1" : $1;
            }


            if ( $event =~ m| acct="([^"]+)"| ) {
                $sh{$auditid}{acct} = $1;
            } elsif ( $event =~ m| acct=([A-F0-9]+)| ) {
                $sh{$auditid}{acct} = pack( "H*", "$1" );
            } elsif ( $event =~ m| acct=(\S+)| ) {
                $sh{$auditid}{acct} = $1;
            }

            if ( $event =~ m| cmd="([^"]+)"| ) {
                $sh{$auditid}{cmd} = $1;
            } elsif ( $event =~ m| cmd=([A-F0-9]+)| ) {
                $sh{$auditid}{cmd} = pack( "H*", "$1" );
            } elsif ( $event =~ m| cmd=(\S+)| ) {
                $sh{$auditid}{cmd} = $1;
            }

            if ( $event =~ m|[' ]cwd="([^"]+)"| ) {
                $sh{$auditid}{cwd} = $1;
            } elsif ( $event =~ m|[' ]cwd=([A-F0-9]+)| ) {
                $sh{$auditid}{cwd} = pack( "H*", "$1" );
            } elsif ( $event =~ m|[' ]cwd=(\S+)| ) {
                $sh{$auditid}{cwd} = $1;
            }

            if ( $event =~ m|[' ]unit="([^"]+)"| ) {
                $sh{$auditid}{unit} = $1;
            } elsif ( $event =~ m|[' ]unit=([A-F0-9]+)| ) {
                $sh{$auditid}{unit} = pack( "H*", "$1" );
            } elsif ( $event =~ m|[' ]unit=(\S+)| ) {
                $sh{$auditid}{unit} = $1;
            }

            if ( $event =~ m| exe="([^"]+)"| ) {
                $sh{$auditid}{exe} = $1;
            } elsif ( $event =~ m| exe=([A-F0-9]+)| ) {
                $sh{$auditid}{exe} = pack( "H*", "$1" );
            } elsif ( $event =~ m| exe=(\S+)| ) {
                $sh{$auditid}{exe} = $1;
            }

            if ( $event =~ m|[' ]op="([^"]+)"| ) {
                $sh{$auditid}{op} = $1;
            } elsif ( $event =~ m|[' ]op=([A-F0-9]+)| ) {
                $sh{$auditid}{op} = pack( "H*", "$1" );
            } elsif ( $event =~ m|[' ]op=(\S+)| ) {
                $sh{$auditid}{op} = $1;
            }

            if ( $event =~ m| vm="([^"]+)"| ) {
                $sh{$auditid}{vm} = $1;
            } elsif ( $event =~ m| vm=([A-F0-9]+)| ) {
                $sh{$auditid}{vm} = pack( "H*", "$1" );
            } elsif ( $event =~ m| vm=(\S+)| ) {
                $sh{$auditid}{vm} = $1;
            }

            if ( $event =~ m| proctitle="([^"]+)"| ) {
                $sh{$auditid}{proctitle} = $1;
            } elsif ( $event =~ m| proctitle=([A-F0-9]+)| ) {
                $sh{$auditid}{proctitle} = pack( "H*", "$1" );
                $sh{$auditid}{proctitle} =~ tr/a-zA-Z0-9\-_\[\]\{\}\.,\<\>\?\/ \!\@\#\$\%\^\&\*\(\);:/ /dc;
            } elsif ( $event =~ m| proctitle=(\S+)| ) {
                $sh{$auditid}{proctitle} = $1;
            }

            if ( $event =~ m| exit=-?(\d+)| ) {
                if ( defined $errcode{$1} ) {
                    $sh{$auditid}{errcode} = $errcode{$1};
                    $sh{$auditid}{errdesc} = $errdesc{ $sh{$auditid}{errcode} };
                }
            }
            if ( $event =~ m| saddr=([A-F0-9]+)| ) {
                &xlate_saddr( $1, \$sh{$auditid}{saddr}, \$sh{$auditid}{port} );
            }

            if ( $event =~ m| argc=(\d+)| ) {
                $argc = $1;
                $args = $1 if ( $event =~ m| argc=\d+( .*)| );
                if ( $argc > ( defined $sh{$auditid}{argc} ? $sh{$auditid}{argc} : 0 ) ) {

                    # Sometimes there are more than 1 EXECVE lines with missing args.
                    # Choose the highest argc.
                    $sh{$auditid}{argc} = $argc;
                    @command = split( /\s+a\d+=/, $args );
                    @cleancmd = '';
                    foreach $cmd (@command) {
                        if ( $cmd =~ m|^([A-F0-9]+)$| ) {
                            $cmd = pack( "H*", "$1" );
                        }
                        $cmd =~ s/(^"|"$|^\s+|\s+$)//g;
                        push( @cleancmd, $cmd );
                    }
                    $sh{$auditid}{command} = join( ' ', @cleancmd );
                    $sh{$auditid}{command} =~ s/(^\s+|\s+$)//g;
                    $sh{$auditid}{command} =~ s|\n| |g;
                    $sh{$auditid}{command} =~ s|\s+| |g;
                }
            }

        }

      AUDITID:
        foreach $auditid ( sort( keys %sh ) ) {

            # audits do not arrive in order.
            # wait a few seconds before logging.
            $time = time;
            next AUDITID if ( $time < $sh{$auditid}{autime} + 5 );

            # don't log myself
            if ( ( $sh{$auditid}{pid} == $$ ) or ( $sh{$auditid}{ppid} == $$ ) ) {
                delete $sh{$auditid};
                next AUDITID;
            }

            foreach $ignorekey ( keys %ignores ) {
                foreach $ignorestring ( @{ $ignores{$ignorekey} } ) {
                    if ( $sh{$auditid}{$ignorekey} =~ m|$ignorestring| ) {
                        delete $sh{$auditid};
                        next AUDITID;
                    }
                }
            }

            $log = '';
            if ( $sh{$auditid}{types} =~ m|EXECVE| ) {
                undef @alarms;
                foreach $name ( keys %{ $sh{$auditid}{ouid} } ) {
                    if ( ( $sh{$auditid}{uid} != $sh{$auditid}{ouid}{$name} )
                    and ( $sh{$auditid}{ouid}{$name} != 0 ) ) {
                        $ouid_user = getpwuid($sh{$auditid}{ouid}{$name}) || 'unset';
                        push(@alarms, "$sh{$auditid}{uid}:$sh{$auditid}{uid_user} ran $name owned by $sh{$auditid}{ouid}{$name}:$ouid_user");
                    }
                }
                foreach $name ( keys %{ $sh{$auditid}{mode} } ) {
                    if ( ( $sh{$auditid}{mode}{$name} =~ m|[267]$| )
                    and  ( $sh{$auditid}{mode}{$name} !~ m|01[24]0[67][67][67]| ) ) {
                        push(@alarms, "$name is mode $sh{$auditid}{mode}{$name} world-writable");
                    }
                }
                if ( @alarms ) {
                    $sh{$auditid}{ALARMS} = join(', ', @alarms);
                }
            }
            foreach $subkey ( sort ( keys %{ $sh{$auditid} } ) ) {
                next if ( $subkey eq "argc" );
                next if ( $subkey eq "autime" );
                next if ( $subkey eq "item" );
                next if ( $subkey eq "mode" );
                next if ( $subkey eq "ouid" );
                next if ( $sh{$auditid}{$subkey} =~ m|^\??$|g );
                $sh{$auditid}{$subkey} =~ s|"||g;
                $log .= qq($subkey="$sh{$auditid}{$subkey}" );
            }
            $log .= qq(\n);
            open( LOGFILE, ">>", "$logfile" ) and do {
                print LOGFILE $log;
                close(LOGFILE);
            };
            delete $sh{$auditid};

        }
    }

    return 0;
}

#################################################################################
sub xlate_saddr {
    my $saddr   = shift;
    my $r_saddr = shift;
    my $r_port  = shift;
    my $fam1;
    my $fam2;
    my $family;
    my $port1;
    my $port2;
    my @addr;
    my $port;
    my @ipparts;
    my $ip;
    my $junk;
    my $ip6;
    my @ip6a;

    ( $fam1, $fam2, $port1, $port2, @addr ) = unpack( "A2A2A2A2A2A2A2A2", $saddr );
    $family = hex($fam1) + 256 * hex($fam2);
    if ( $family == 10 ) {
        ( $junk, $port1, $port2, $junk, $ip6 ) = unpack( "A4A2A2A8A32", $saddr );
        $port = 256 * hex($port1) + hex($port2);
        @ip6a = split( //, $ip6 );
        if ( "@ip6a[20 .. 23]" eq "F F F F" ) {

            # is really ipv4 in ipv6 notation
            ( $junk, @addr ) = unpack( "A40A2A2A2A2", $saddr );
            $ip       = hex( $addr[0] ) . '.' . hex( $addr[1] ) . '.' . hex( $addr[2] ) . '.' . hex( $addr[3] );
            $$r_saddr = ':::::FFFF:' . $ip;
            $$r_port  = $port;
        } else {
            $ip6 = "@ip6a[0 .. 3]" . ':';
            $ip6 .= "@ip6a[4 .. 7]" . ':';
            $ip6 .= "@ip6a[8 .. 11]" . ':';
            $ip6 .= "@ip6a[12 .. 15]" . ':';
            $ip6 .= "@ip6a[16 .. 19]" . ':';
            $ip6 .= "@ip6a[20 .. 23]" . ':';
            $ip6 .= "@ip6a[24 .. 27]" . ':';
            $ip6 .= "@ip6a[28 .. 31]";
            $ip6 =~ s/\s+//g;
            $ip6 =~ s/0000//g;
            $$r_saddr = $ip6;
            $$r_port  = $port;
        }
    } elsif ( $family == 2 ) {
        $port     = 256 * hex($port1) + hex($port2);
        $ip       = hex( $addr[0] ) . '.' . hex( $addr[1] ) . '.' . hex( $addr[2] ) . '.' . hex( $addr[3] );
        $$r_saddr = $ip;
        $$r_port  = $port;
    } elsif ( $family == 1 ) {
        $saddr =~ s/(^01..|00.+$)//g;
        $$r_saddr = pack( "H*", "$saddr" );
    }
    if ( $saddr == 100000000000000000000000 ) {
        $$r_saddr = 'netlink pid:0';
    }
    return 0;
}

#################################################################################

__END__

README
This program is an audisp plugin that will make audit events from the Linux Audit daemon human readable.
It will look for execve, socketcall, bind, and connect system calls as well as filesystem changes (if you setup audit rules) and consolidate the events into a simple log format.
The log is in key=value format for easy consumption from Splunk or other log analytic software.
Here is a Splunk query that I run: source="/var/log/audisp-simplify"  |table time key tty ppid auid_user uid_user cwd exe command |sort _time desc 
The logs include the auid_user, which is the original login id.  This is beneficial for seeing who made the syscalls after su'ing to another user such as root.

Here is an execve syscall example of a shell command, ps -efww, executed as myself after su'ing to root:
auditid="213706" auid="1000" auid_user="mkirby" command="ps -efww" cwd="/root" date="2017-02-02" euid="0" euid_user="root" exe="/usr/bin/ps" exit="0" key="EXECVE" pid="4644" ppid="4572" proctitle="ps -efww" ses="814" subj="unconfined_u:unconfined_r:unconfined_t:s0-s0:c0.c1023" time="01:28:19+0000" tty="pts3" types="SYSCALL,EXECVE,CWD,PATH,PROCTITLE,EOE" uid="0" uid_user="root" 


Here is an example of /etc/hosts being modified.  Error code ESRCH means someone echo'd into the file.
auditid="8688" auid="1000" auid_user="mkirby" cwd="/root" date="2017-02-02" errcode="ESRCH" errdesc="No such process" euid="0" euid_user="root" exe="/usr/bin/bash" exit="3" key="FILE" name="/etc/hosts" pid="5884" ppid="5883" proctitle="-bash" ses="4" subj="unconfined_u:unconfined_r:unconfined_t:s0-s0:c0.c1023" time="02:12:14+0000" tty="pts2" types="SYSCALL,CWD,PATH,PROCTITLE,EOE" uid="0" uid_user="root"

Here is a connect syscall example of an egress connection to mkirby.org:
auditid="9845" auid="1000" auid_user="mkirby" date="2017-02-02" euid="0" euid_user="root" exe="/usr/bin/telnet" exit="0" key="CONNECT" pid="14649" port="80" ppid="5884" proctitle="telnet localhost 80" saddr="127.0.0.1" ses="4" subj="unconfined_u:unconfined_r:unconfined_t:s0-s0:c0.c1023" time="02:14:29+0000" tty="pts2" types="SYSCALL,SOCKADDR,PROCTITLE,EOE" uid="0" uid_user="root"

Here is a bind syscall example of httpd binding to port 80.
auditid="1165" auid="4294967295" date="2017-02-01" euid="0" euid_user="root" exe="/usr/sbin/httpd" exit="0" key="BIND" pid="3037" port="80" ppid="1" proctitle="(httpd)" saddr=":::::::" ses="4294967295" time="16:47:49+0000" tty="(none)" type="EOE" uid="0" uid_user="root"




INSTALLATION for RedHat, Centos, Scientific Linux, and Fedora

1) Place this script file in /bin/ and chmod this file to 750

2) Install the audit and audispd-plugins packages

3) Enable auditd  on bootup and start the service

4) Create a new file, /etc/audit/plugins.d/simplify.conf (/etc/audisp/plugins.d if rhel7) and add the following:
    active = yes
    direction = out
    path = /bin/audisp-simplify
    type = always
    format = string

5) Increase queue in /etc/audisp/audispd.conf and set overflow_action to ignore
    q_depth = 655360
    overflow_action = ignore
    priority_boost = 4
    max_restarts = 10
    name_format = HOSTNAME
    #
    # use /etc/audisp/plugins.d if rhel7
    plugin_dir = /etc/audit/plugins.d/

6) Replace /etc/audit/rules.d/audit.rules with the following: (you may want to add/del to dir monitoring).  YOU NEED A KEY DEFINED ( -k ) for audisp-simplify to log the event.
    # delete all existing rules
    -D
    # disable auditing during load
    -e 0
    # fail silently
    -f 0
    # 65k buffer
    -b 65536
    # no rate
    -r 0
    # continue loading if bad rule and report
    -c
    #
    # Add any other dirs you want monitored for file writes
    # These can be noisy during patching. 
    -w /etc/                 -p w  -k FILE-etc
    -w /var/spool/cron/      -p w  -k FILE-cron
    -w /var/www/             -p w  -k FILE-www
    -w /var/named/chroot/    -p w  -k FILE-named
    -w /boot/                -p w  -k FILE-boot
    -w /root/.ssh/           -p rw -k FILE-ssh
    -w /etc/pki/tls/private/ -p r  -k FILE-pki
    -w /etc/pki/tls/certs/   -p r  -k FILE-pki
    #-w /usr/                 -p w  -k FILE-usr
    #
    # Monitor commands.  I add "-F uid!=setroubleshoot" to mine to avoid selinux junk.
    -a exit,always -F arch=b32 -F exit=0 -S execve -k EXECVE
    -a exit,always -F arch=b64 -F exit=0 -S execve -k EXECVE
    #
    # Monitor network connections.
    # These are VERY noisy.  Enable at your own risk
    #-a exit,always -F arch=b32 -F exit=0 -S socketcall -k SOCKETCALL
    #-a exit,always -F arch=b64 -F exit=0 -S bind -k BIND
    #-a exit,always -F arch=b64 -F exit=0 -S connect -k CONNECT
    #
    ## This rule suppresses the time-change event when chrony does time updates
    -a never,exit -F arch=b64 -S adjtimex -F auid=unset -Fuid=chrony
    -a never,exit -F arch=b32 -S adjtimex -F auid=unset -Fuid=chrony
    -a never,exit -F arch=b64 -S adjtimex -F auid=unset -Fuid=ntp
    -a never,exit -F arch=b32 -S adjtimex -F auid=unset -Fuid=ntp


    #Record Attempts to Alter Logon and Logout Events
    -w /var/log/faillog -p w -k LOGINS-log
    -w /var/log/lastlog -p w -k LOGINS-log

    #Record Attempts to Alter Process and Session Initiation Information
    -w /var/run/utmp -p w -k SESSION-log
    -w /var/log/btmp -p w -k SESSION-log
    -w /var/log/wtmp -p w -k SESSION-log

    #Ensure auditd Collects Information on Kernel Module Loading and Unloading
    -w /sbin/insmod -p x -k MODULES
    -w /sbin/rmmod -p x -k MODULES
    -w /sbin/modprobe -p x -k MODULES
    -a always,exit -F arch=b32 -S init_module,finit_module -k MODULES
    -a always,exit -F arch=b64 -S init_module,finit_module -k MODULES
    -a always,exit -F arch=b32 -S delete_module -k MODULES
    -a always,exit -F arch=b64 -S delete_module -k MODULES

    ## These rules watch for code injection by the ptrace facility.
    ## This could indicate someone trying to do something bad or
    ## just debugging
    -a always,exit -F arch=b32 -S ptrace -k PTRACE-tracing
    -a always,exit -F arch=b64 -S ptrace -k PTRACE-tracing
    -a always,exit -F arch=b32 -S ptrace -F a0=0x4 -k PTRACE-code-injection
    -a always,exit -F arch=b64 -S ptrace -F a0=0x4 -k PTRACE-code-injection
    -a always,exit -F arch=b32 -S ptrace -F a0=0x5 -k PTRACE-data-injection
    -a always,exit -F arch=b64 -S ptrace -F a0=0x5 -k PTRACE-data-injection
    -a always,exit -F arch=b32 -S ptrace -F a0=0x6 -k PTRACE-register-injection
    -a always,exit -F arch=b64 -S ptrace -F a0=0x6 -k PTRACE-register-injection

    # activate auditing
    -e 1


7) Setup log rotation by creating /etc/logrotate.d/audisp-simplify 
   Add the following:
        /var/log/audisp-simplify
        {
        rotate 30
        daily
        create
        compress
        dateext
        dateyesterday
        shred
        sharedscripts
        create 0600 root root
        postrotate
            /sbin/service auditd restart >/dev/null 2>&1 || true
            /bin/systemctl reload auditd.service >/dev/null 2>&1 || true
        endscript
        }

8) [OPTIONAL] Setup an ignores file for strings that you don't want logged.
    Create a new file /etc/audisp/simplify.ignores and use key=value pairs to specify what you don't want logged.
    The string values can be in Perl regex format.
    Here is an example of my file:
        saddr=netlink.*
        saddr=public
        saddr=private
        saddr=/dev/log
        saddr=:::::::
        name=.*swx$
        name=.*swp$
        name=.*swpx$
        exe=/var/ossec/bin/ossec-syscheckd
        exe=/opt/splunk/bin/splunkd
        exe=/opt/splunkforwarder/bin/splunkd
        
9) Restart the auditd service

10) If you are seeing audit logs in journald, you can disable it with systemctl mask systemd-journald-audit.socket

11) Done.  Now you can watch the simple audit logs in /var/log/audisp-simplify





AFTER INSTALL
1) Keep an eye on the audit queue with 'auditctl -s'.  You may need to tune audit if the lost and backlog events increase

2) Keep an eye on your syslog for errors from audispd.  You may need to increase the q_depth and priority_boost in /etc/audisp/audispd.conf

3) If you have an insanely busy uid, you can add "-F uid!=<uid>" to execve in audit.rules





INSTALLATION ADDENDUM FOR SELINUX
If you are running SELinux, you may want to add the following to your local policy.
1) First make sure this directory exists: /etc/selinux/targeted/policy/.

2) Add these lines to /etc/selinux/targeted/policy/local.te
module local 1.0;
require {
	class dir { open getattr search write read remove_name add_name };
	class file { create open read write execute execute_no_trans getattr };
	class chr_file { open read getattr };
	type audisp_t;
	type auditd_t;
	type auditd_etc_t;
	type auditd_log_t;
	type urandom_device_t;
	type var_log_t;
}  
allow auditd_t var_log_t:file { create open read write execute execute_no_trans getattr };
allow audisp_t var_log_t:file { create open read write execute execute_no_trans getattr };
allow audisp_t var_log_t:dir { write add_name };
allow audisp_t auditd_etc_t:dir { read search open };
allow audisp_t auditd_etc_t:file { read open getattr };
allow audisp_t auditd_log_t:dir { read search open };
allow audisp_t auditd_log_t:file { read open getattr };
allow audisp_t urandom_device_t:chr_file { read open getattr };

3) Then run
    cd /etc/selinux/targeted/policy/
    checkmodule -M -m -o local.mod local.te
    semodule_package -o local.pp -m local.mod
    semodule -i local.pp


